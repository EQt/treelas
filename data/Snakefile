from os import path
import pandas as pd


if path.exists("./config.yml"):
    configfile: "./config.yml"
elif path.exists("../config.yml"):
    configfile: "../config.yml"


CMAKE_BUILD = config["cmake_dir"]
PARAMS = dict(s=range(config['num_span_trees']),
              l=config["lam"],
              i=config['num_repetitions'],
              alg=["opt", "apx"])
MAX_ITER = config['apx_max_iter']


subworkflow dimacs:
    workdir: "dimacs10"
    configfile: "./config.yml"


subworkflow snap:
    workdir: "snap"
    configfile: "./config.yml"


subworkflow random:
    workdir: "random"
    configfile: "./config.yml"


subworkflow image:
    workdir: "image"
    configfile: "./config.yml"


rule all_degs:
    input:
        random("binary_n1e8.degs"),
        random("randmix_n5e7_f1e-7.degs"),
        image("phantom_w1000.degs"),
        dimacs("europe.degs"),
        snap("com-orkut.degs"),


rule all:
    input:
        random("binary_n1e8.tsv"),
        random("randmix_n5e7_f1e-7.tsv"),
        image("phantom_w1000.tsv"),
        dimacs("europe.tsv"),
        snap("com-orkut.tsv"),


rule graph_degrees:
    input:  "{name}_0.tree"
    output: "{name}.degs"
    run:
        from degrees import print_deg_stats

        with open(output[0], "w") as io:
            print_deg_stats(input[0], out=io)


rule collect_times:
    input: expand("{{name}}_{s}_lam{l}_rep{i}.tree_{alg}.times", **PARAMS)
    output: "{name}.tsv"
    run:
        from extract_times import tsv_concat

        tsv_concat(output[0], input)


rule extract_times:
    input:  "{name}_{s}_lam{l}_rep{i}.tree_{alg}"
    output: "{name}_{s}_lam{l}_rep{i}.tree_{alg}.times"
    run:
        from extract_times import extract_time_n

        time_pn, n = extract_time_n(input[0])
        df = pd.DataFrame(data=dict(
            dataset=wildcards.name.rpartition('/')[-1],
            seed=wildcards.s,
            lam=wildcards.l,
            rep=wildcards.i,
            algorithm=f'tree_{wildcards.alg}',
            num_nodes=n,
            time_per_node=time_pn,
        ), index=[0])
        df.to_csv(output[0], sep="\t", index=None)


rule bench_dimacs:
    input:
        dimacs(expand("belgium_{s}_lam{l}_rep{i}.tree_{alg}", **PARAMS))


rule run_treelas:
    input: "{name}.treelas"
    output: "{name}_lam{lam,[^_]+}{ignore}.tree_opti", "{name}_lam{lam,[^_]+}{ignore}.tree_sol"
    shell: """
    {CMAKE_BUILD}/tree_solve -l {wildcards.lam} {input} | tee {output[0]} && cp {input} {output[1]}
    """


rule run_treelas_no_output:
    input: "{name}.treelas"
    output: "{name}_lam{lam,[^_]+}{ignore}.tree_opt"
    shell: """
    {CMAKE_BUILD}/tree_solve -O -l {wildcards.lam} {input} | tee {output}
    """


rule run_treeapx:
    input: "{name}.treelas"
    output: "{name}_lam{lam,[^_]+}{ignore}.tree_apx"
    shell: "{CMAKE_BUILD}/tree_apx -r -i {MAX_ITER} -l {wildcards.lam} {input} | tee {output}"


rule spanning_tree:
    input: "{name}.h5"
    output: "{name}_{seed,\\d+}.tree"
    shell: """
    {CMAKE_BUILD}/traverse -s {wildcards.seed} {input} {output}
    """


rule noisy_input:
    input: "{name}{seed}.tree"
    output: "{name}{seed,_\\d+}.treelas"
    run:
        import numpy as np
        import h5py
        from shutil import copyfile

        seed = wildcards.seed
        seed = int(seed[1:]) if seed[0] == "_" else 2020
        np.random.seed()
        copyfile(input[0], output[0])
        y = None
        img_fn = f"{wildcards.name}.img"
        if path.exists(img_fn):
            with h5py.File(img_fn, "r") as io:
                y = io['y'][:]
        with h5py.File(output[0], "r+") as io:
            n = len(io["parent"])
            if y is None:
                y = np.random.randn(n)
            io.create_dataset("y", data=y)
            io.create_dataset("lam", data=[0.1])
