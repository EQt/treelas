from snakemake.remote.HTTP import RemoteProvider as HTTPRemoteProvider

HTTP = HTTPRemoteProvider()
WIKI = "https://upload.wikimedia.org/wikipedia/commons"


rule download_all:
    input:
        'phantom.svg'


rule download_phantom:
    output:
        'phantom.svg'
    input:
        HTTP.remote(WIKI + "/c/c7/SheppLogan_Phantom.svg", keep_local=False)
    run:
        from decss import remove_css

        remove_css(input[0], output[0])


rule phantom_demo:
    input: "phantom_w300.img"


rule render_svg:
    input:  '{name}.svg'
    output: '{name}_w{width,\d+}.img'
    run:
        from render_svg import gm_render
        import h5py
        import numpy as np

        seed = 2020
        std = 0.25
        np.random.seed(seed)
        orig = gm_render(input[0], width=int(wildcards.width))
        y = orig + np.random.normal(scale=std, size=orig.shape)
        with h5py.File(output[0], 'w') as io:
            io.create_dataset('orig', data=orig)
            io.create_dataset('y', data=y)
            io.create_dataset("lam", data=[0.1])


rule grid_graph:
    input:  '{name}.img'
    output: '{name}.graph'
    shell: "julia grid.jl {input} {output}"


rule grid_spanning_tree:
    input: "{name}.graph"
    output: "{name}_{seed,\\d+}.tree"
    shell: """
    {CMAKE_BUILD}/traverse -s {wildcards.seed} {input} {output}
    """


include: "../Snakefile"
